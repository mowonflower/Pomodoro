#include <ESP8266WiFi.h>
#include <NTPClient.h>
#include <WiFiUdp.h>
#include <Adafruit_NeoPixel.h>

#define LED_PIN D1 // Make sure this PIN is right!
#define BUTTON_RED_PIN D2 // Make sure this PIN is right!
#define BUTTON_BLACK_PIN D3 // Make sure this PIN is right!
#define STRIPSIZE 12

const char *ssid     = "WiFi-Netzwerk";
const char *password = "WiFi-Passwort";

//Zeitverschiebung UTC <-> MEZ (Winterzeit) = 3600 Sekunden (1 Stunde)
//Zeitverschiebung UTC <-> MEZ (Sommerzeit) = 7200 Sekunden (2 Stunden)
const long utcOffsetInSeconds = 3600;

const int twoMin = 120;
const int oneMin = 60;
int currentTime = 0;

WiFiUDP ntpUDP;
NTPClient timeClient(ntpUDP, "pool.ntp.org", utcOffsetInSeconds);
Adafruit_NeoPixel strip = Adafruit_NeoPixel(STRIPSIZE, LED_PIN, NEO_GRB + NEO_KHZ800);

void setup(){
  Serial.begin(115200);

  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.println("Ich verbinde mich mit dem Internet...");
  }
  
  Serial.println("Ich bin mit dem Internet verbunden!");

  timeClient.begin();

  pinMode(BUTTON_RED_PIN, INPUT_PULLUP); // Initialize Red Button
  pinMode(BUTTON_BLACK_PIN, INPUT_PULLUP); // Initialize Black Button
  strip.begin();
  strip.setBrightness(50);  // Lower brightness and save eyeballs!
  strip.show(); // Initialize all pixels to 'off'
}

void loop() {
  if(!digitalRead(BUTTON_BLACK_PIN)){
    timeClient.update();
    currentTime = timeClient.getEpochTime();
    for(int i=0; i < 4; i++){
      switch(i){
        case 0:
          FocusTime(255,0,0);
          break;

        case 1:
          FocusTime(0,255,0);
          break;
        
        case 2:
          FocusTime(0,0,255);
          break;

        case 3:
          FocusTime(255,0,255);
          break;

        default:
          break;
      }
      if(!digitalRead(BUTTON_RED_PIN))
        break;
      if(i!=3)
        PauseTime();
    }
    End();
  }

}

void colorFill(int r, int g, int b) {
  for(int i=0; i < strip.numPixels(); i++) {
      strip.setPixelColor(i, strip.Color(r, g, b));
      strip.show();
  }
}

void FocusTime(int r, int g, int b){
  for(int i=0; i < 12; i++){
    strip.setPixelColor(i, strip.Color(r,g,b));
    strip.show();
    if(!digitalRead(BUTTON_RED_PIN))
        break;
    Wait(twoMin);
  }
  colorFill(0,0,0);
}

void PauseTime(){
  for(int i=0; i < 5; i++){
    strip.setPixelColor(i, strip.Color(255,255,255));
    strip.show();
  }
  for(int i=0; i < 5; i++){
    if(!digitalRead(BUTTON_RED_PIN))
        break;
    Wait(oneMin);
    strip.setPixelColor(4 - i, strip.Color(0,0,0));
    strip.show();
  }
}

void End(){
  for(int i=0; i < 2; i++){
    colorFill(255,255,255);
    delay(500);
    colorFill(0,0,0);
    delay(500);
  }
}

void Wait(int seconds){
  int preDelay = currentTime;
  while(currentTime < preDelay + seconds){
    delay(1000);
    if(!digitalRead(BUTTON_RED_PIN))
        break;
    timeClient.update();
    currentTime = timeClient.getEpochTime();
  }
}
